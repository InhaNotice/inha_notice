plugins {
    id "com.android.application"
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('app/key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

def versionProperties = new Properties()
def versionPropertiesFile = rootProject.file('app/version.properties')
if (!versionPropertiesFile.exists()) {
    throw new GradleException("app/version.properties 파일이 없습니다.")
}
versionProperties.load(new FileInputStream(versionPropertiesFile))
def getRequiredProp = { key ->
    def val = versionProperties.getProperty(key)
    if (val == null) throw new GradleException("version.properties에 '${key}'가 없습니다.")
    return val
}

android {
    namespace = "com.logicallawbio.inha_notice"
    compileSdk = 36
    ndkVersion = "29.0.14033849"
    buildToolsVersion = "36.0.0"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_21
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId = getRequiredProp('APPLICATION_ID')
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdkVersion = flutter.minSdkVersion
        targetSdk = 36
        versionCode = Integer.parseInt(getRequiredProp('VERSION_CODE'))
        versionName = getRequiredProp('VERSION_NAME')
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        debug {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = "inha-notice-${variant.buildType.name}.apk"
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:33.9.0')
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.4'
}
